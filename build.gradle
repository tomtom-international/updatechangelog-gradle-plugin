plugins {
  id 'com.jfrog.bintray' version '1.8.4'
  id 'java-gradle-plugin'
  id 'net.researchgate.release' version '2.5.0'
}

apply plugin: 'com.jfrog.bintray'
apply plugin: 'groovy'
apply plugin: 'java-gradle-plugin'
apply plugin: 'maven-publish'
apply from: "$rootDir/gradle/functional-test.gradle"


gradlePlugin {
  plugins {
    updateChangelogPlugin {
      id = 'com.tomtom.gradle.updatechangelog'
      description = 'A plugin that automatically updates the version in your changelog file.'
      implementationClass = 'com.tomtom.gradle.updatechangelog.UpdateChangelogPlugin'
    }
  }
}

repositories {
  jcenter()
}

dependencies {
  compile gradleApi()
  compile localGroovy()
  testCompile('org.junit.jupiter:junit-jupiter-api:5.3.1')
  testRuntime('org.junit.jupiter:junit-jupiter-engine:5.3.1')

  testCompile('org.spockframework:spock-core:1.2-groovy-2.4') {
    exclude group: 'org.codehaus.groovy'
  }
  // This dependency is required by the spockframework since
  // it is still based on JUnit4.
  testRuntimeOnly "org.junit.vintage:junit-vintage-engine:5.3.1"
}

test {
  useJUnitPlatform()
  testLogging {
    events 'passed', 'skipped', 'failed'
  }
  reports {
    html.enabled = true
    junitXml.enabled = true
  }
}

compileGroovy {
  targetCompatibility = JavaVersion.VERSION_1_8
  options.encoding = 'UTF-8'
  options.compilerArgs = ['-Xlint:all']
  options.deprecation = true
  options.warnings = true
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
      groupId = 'com.tomtom.gradle'
    }
  }
}

bintray {
  user = project.findProperty('bintray.user') ?: System.getProperty('bintray.user')
  key = project.findProperty('bintray.key') ?: System.getProperty('bintray.key')
  publications = ['mavenJava']
  publish = true //[Default: false] Whether version should be auto published after an upload

  pkg {
    repo = 'gradle-plugins'
    name = 'updatechangelog-gradle-plugin'
    userOrg = 'tomtom-international'
    desc = 'A plugin that automatically updates the version in your changelog file.'
    websiteUrl = 'https://github.com/tomtom-international/updatechangelog-gradle-plugin'
    issueTrackerUrl = 'https://github.com/tomtom-international/updatechangelog-gradle-plugin/issues'
    vcsUrl = 'https://github.com/tomtom-international/updatechangelog-gradle-plugin'
    publicDownloadNumbers = true
    licenses = ['Apache-2.0']
    labels = ['changelog', 'release']

    version {
      name = "${project.version}"
      desc = "Gradle Update Changelog Plugin ${project.version}"
      vcsTag = "${project.version}"
      released = new Date()
    }
  }
}

wrapper {
  gradleVersion = '4.10.2'
}

ext.'gradle.release.useAutomaticVersion' = "true"
afterReleaseBuild.dependsOn bintrayUpload
