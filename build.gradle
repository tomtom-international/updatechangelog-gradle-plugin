plugins {
  id 'com.gradle.plugin-publish' version '0.13.0'
  id 'java-gradle-plugin'
  id 'net.researchgate.release' version '2.7.0'
  id 'com.tomtom.gradle.updatechangelog' version '1.0.6'
  id 'groovy'
  id 'maven-publish'
}

apply from: "$rootDir/gradle/functional-test.gradle"

group = 'com.tomtom.gradle'

gradlePlugin {
  plugins {
    updateChangelog {
      id = "${project.group}.updatechangelog"
      implementationClass = "${project.group}.updatechangelog.UpdateChangelogPlugin"
    }
  }
}

repositories {
  mavenCentral()
  gradlePluginPortal()
}

dependencies {
  compile gradleApi()
  compile localGroovy()
  testCompile('org.junit.jupiter:junit-jupiter-api:5.3.1')
  testRuntime('org.junit.jupiter:junit-jupiter-engine:5.3.1')

  testCompile('org.spockframework:spock-core:1.2-groovy-2.4') {
    exclude group: 'org.codehaus.groovy'
  }
  // This dependency is required by the spockframework since
  // it is still based on JUnit4.
  testRuntimeOnly "org.junit.vintage:junit-vintage-engine:5.3.1"
}

test {
  useJUnitPlatform()
  testLogging {
    events 'passed', 'skipped', 'failed'
  }
  reports {
    html.enabled = true
    junitXml.enabled = true
  }
}

compileGroovy {
  targetCompatibility = JavaVersion.VERSION_1_8
  options.encoding = 'UTF-8'
  options.compilerArgs = ['-Xlint:all']
  options.deprecation = true
  options.warnings = true
}

pluginBundle {
  website = 'https://github.com/tomtom-international/updatechangelog-gradle-plugin'
  vcsUrl = 'https://github.com/tomtom-international/updatechangelog-gradle-plugin'
  description = 'A plugin that automatically updates the version in your changelog file.'

  plugins {
    updateChangelog {
      displayName = 'Gradle changelog update plugin'
      tags = ['changelog', 'release']
      version = "${project.version}"
    }
  }
}

changelog {
  versionPlaceholder = '# Changelog'
  changelogFilename = 'CHANGELOG.md'
}

afterReleaseBuild.dependsOn publishPlugins
preTagCommit.dependsOn updateChangelog
